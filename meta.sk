function inLobby(p: player) :: boolean:
    if metadata "state" of {_p} is "lobby":
        return true
    else if metadata "state" of {_p} is "queued":
        send "&4&lGENRES &8| &7You are unable to join this game because you are in queue." to {_p}
        return false
    else:
        send "&4&lGENRES &8| &7You are unable to join this game because you are already in a game." to {_p}
        return false

function toLobby(p: player):
    send "&4&lGENRES &8| &7You have left the world!" to {_p}
    set metadata "state" of {_p} to "lobby"
    clean({_p})
    tpWorld({_p}, "world")
    set {_p}'s gamemode to survival

function quit(p: player):
    if metadata "state" of {_p} is "lobby":
        send "&4&lGENRES &8| &7You have nowhere else to go!" to {_p}
        exit
    if metadata "state" of {_p} is "queued":
        remove {_p} from {-queue.stick::*}
        send "&4&lGENRES &8| &7You left the queue." to {_p}
        set metadata "state" of {_p} to "lobby"
        exit
    if metadata "state" of {_p} is "straight":
        delWorld("%{_p}%")
        delete {time.%{_p}%}
    if metadata "state" of {_p} is "diagonal":
        delWorld("%{_p}%")
        delete {time.%{_p}%}
    if metadata "state" of {_p} is "stick":
        set {_enemy} to {enemy.%{_p}%}
        delete {enemy.%{_p}%}
        delete {enemy.%{_enemy}%}
        delete {lives.%{_p}%}
        delete {lives.%{_enemy}%}
        delete {loc.%{_p}%}
        delete {loc.%{_enemy}%}
        if "%{_p}'s world%" contains "%{_p}%":
            delWorld("%{_p}'s world%")
        broadcast "&4&lGENRES &8| &c%{_enemy}%&7 won a stick match."
        toLobby({_enemy})
    if metadata "state" of {_p} is "sumo":
        set {_enemy} to {enemy.%{_p}%}
        delete {enemy.%{_p}%}
        delete {enemy.%{_enemy}%}
        if "%{_p}'s world%" contains "%{_p}%":
            delWorld("%{_p}'s world%")
        broadcast "&4&lGENRES &8| &c%{_enemy}%&7 won a sumo match."
        toLobby({_enemy})
    toLobby({_p})
    


function clearBlocks(p: player):
    loop {-cleanup.%{_p}%::*}:
        set block at loop-value to air
    delete {-cleanup.%{_p}%::*}

function safeTeleport(p: player, l: location):
    set block at {_l} to air
    set block above block at {_l} to air
    teleport {_p} to {_l}