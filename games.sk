function reset_straight(p: player):
    set {time.%{_p}%} to now
    clearBlocks({_p})
    tpWorld({_p}, "%{_p}%")
    clean({_p})
    give 256 of red wool to {_p}

function reset_clutch(p: player):
    set {time.%{_p}%} to now
    clearBlocks({_p})
    tpWorld({_p}, "%{_p}%")
    clean({_p})
    set {_p}'s gamemode to adventure
    give 64 of red wool with nbt "{CanPlaceOn:[""minecraft:white_terracotta""]}" to {_p}

function reset_water(p: player):
    tpWorld({_p}, "%{_p}%")
    clean({_p})
    give {_p} water bucket

function reset_tnt(p: player):
    tpWorld({_p}, "%{_p}%")
    clean({_p})
    give {_p} tnt

function reset_diagonal(p: player):
    set {time.%{_p}%} to now
    clearBlocks({_p})
    tpWorld({_p}, "%{_p}%")
    clean({_p})
    give 256 of red wool to {_p}

function stick(p1: player, p2: player):
    set metadata "state" of {_p1} to "stick"
    set metadata "state" of {_p2} to "stick"
    remove {_p1} from {-queue.stick::*}
    remove {_p2} from {-queue.stick::*}
    set {_world} to "%{_p1}%_%{_p2}%"
    cloneWorld("stick", {_world})
    give stick of knockback 1 to {_p1}
    give stick of knockback 1 to {_p2}
    give 16 of red wool to {_p1}
    give 16 of red wool to {_p2}
    set {enemy.%{_p1}%} to {_p2}
    set {enemy.%{_p2}%} to {_p1}
    set {loc.%{_p1}%} to location(0.5, 3, -20.5, world({_world}))
    set {loc.%{_p2}%} to location(0.5, 3, 20.5, world({_world}), 180, 0)
    teleport {_p1} to {loc.%{_p1}%}
    teleport {_p2} to {loc.%{_p2}%}
    set {_p1}'s gamemode to survival
    set {_p2}'s gamemode to survival
    set {lives.%{_p1}%} to 5
    set {lives.%{_p2}%} to 5
    while metadata "state" of {_p1} is "stick":
        set {_bar} to "&7%{_p1}%: &c%{lives.%{_p1}%}% &8| &7%{_p2}%: &c%{lives.%{_p2}%}%"
        send action bar {_bar} to {_p1}
        send action bar {_bar} to {_p2}
        wait 1 second

function give_pearl(p: player):
    give stick of knockback 1 to {_p}
    give 8 of ender pearl to {_p}
    give 16 of red wool to {_p}
    
function pearl(p1: player, p2: player):
    set metadata "state" of {_p1} to "pearl"
    set metadata "state" of {_p2} to "pearl"
    remove {_p1} from {-queue.pearl::*}
    remove {_p2} from {-queue.pearl::*}
    set {_world} to "%{_p1}%_%{_p2}%"
    cloneWorld("pearl", {_world})
    give_pearl({_p1})
    give_pearl({_p2})
    set {enemy.%{_p1}%} to {_p2}
    set {enemy.%{_p2}%} to {_p1}
    set {loc.%{_p1}%} to location(-46.5, 33, 4.5, world({_world}), -90, 0)
    set {loc.%{_p2}%} to location(-0.5, 33, -0.5, world({_world}), 90, 0)
    teleport {_p1} to {loc.%{_p1}%}
    teleport {_p2} to {loc.%{_p2}%}
    set {_p1}'s gamemode to survival
    set {_p2}'s gamemode to survival
    set {lives.%{_p1}%} to 2
    set {lives.%{_p2}%} to 2
    while metadata "state" of {_p1} is "pearl":
        set {_bar} to "&7%{_p1}%: &c%{lives.%{_p1}%}% &8| &7%{_p2}%: &c%{lives.%{_p2}%}%"
        send action bar {_bar} to {_p1}
        send action bar {_bar} to {_p2}
        wait 1 second


function sumo(p1: player, p2: player):
    set metadata "state" of {_p1} to "sumo"
    set metadata "state" of {_p2} to "sumo"
    remove {_p1} from {-queue.sumo::*}
    remove {_p2} from {-queue.sumo::*}
    set {_world} to "%{_p1}%_%{_p2}%"
    cloneWorld("sumo", {_world})
    set {enemy.%{_p1}%} to {_p2}
    set {enemy.%{_p2}%} to {_p1}
    teleport {_p1} to location(0.5, 3, -5.5, world({_world}), 0, 0)
    teleport {_p2} to location(0.5, 3, 6.5, world({_world}), 180, 0)
    set {_p1}'s gamemode to survival
    set {_p2}'s gamemode to survival

function give_classic(p: player):
    equip {_p} with iron helmet of protection 1
    equip {_p} with iron chestplate of protection 1
    equip {_p} with iron leggings of protection 1
    equip {_p} with iron boots of protection 1
    give {_p} iron sword
    give {_p} fishing rod
    give {_p} bow
    give 5 of arrow to {_p}

function classic(p1: player, p2: player):
    set metadata "state" of {_p1} to "classic"
    set metadata "state" of {_p2} to "classic"
    remove {_p1} from {-queue.classic::*}
    remove {_p2} from {-queue.classic::*}
    set {_world} to "%{_p1}%_%{_p2}%"
    cloneWorld("classic", {_world})
    give_classic({_p1})
    give_classic({_p2})
    set {enemy.%{_p1}%} to {_p2}
    set {enemy.%{_p2}%} to {_p1}
    teleport {_p1} to location(0.5, 65, 18.5, world({_world}), 180, 0)
    teleport {_p2} to location(0.5, 65, -18.5, world({_world}), 0, 0)
    set {_p1}'s gamemode to survival
    set {_p2}'s gamemode to survival

function reset_bedwars(p: player):
    safeTeleport({_p}, {loc.%{_p}%})
    clean({_p})
    give {_p} stick of knockback 1
    give 128 of sandstone to {_p}
    set slot 8 of {_p} to diamond pickaxe of efficiency 2

function bedwars(p1: player, p2: player):
    set metadata "state" of {_p1} to "bedwars"
    set metadata "state" of {_p2} to "bedwars"
    remove {_p1} from {-queue.bedwars::*}
    remove {_p2} from {-queue.bedwars::*}
    set {_world} to "%{_p1}%_%{_p2}%"
    cloneWorld("bedwars", {_world})
    tpWorld({_p1}, {_world})
    tpWorld({_p2}, {_world})
    set {block.%{_p1}%} to blue bed
    set {block.%{_p2}%} to red bed
    set {enemy.%{_p1}%} to {_p2}
    set {enemy.%{_p2}%} to {_p1}
    set {loc.%{_p1}%} to location(-20.5, 5, 0.5, world({_world}), -90, 0)
    set {loc.%{_p2}%} to location(21.5, 5, 0.5, world({_world}), 90, 0)
    reset_bedwars({_p1})
    reset_bedwars({_p2})
    teleport {_p1} to {loc.%{_p1}%}
    teleport {_p2} to {loc.%{_p2}%}
    set {_p1}'s gamemode to survival
    set {_p2}'s gamemode to survival

function reset_bridge(p: player):
    send title colored "&a➄" to {_p}
    play sound "block.note_block.pling" to {_p}
    wait 1 second
    send title colored "&a➃" to {_p}
    play sound "block.note_block.pling" with pitch .9 to {_p} 
    wait 1 second
    send title colored "&a➂" to {_p}
    play sound "block.note_block.pling" with pitch .8 to {_p}
    wait 1 second
    send title colored "&e➁" to {_p}
    play sound "block.note_block.pling" with pitch .7 to {_p}
    wait 1 second
    send title colored "&c➀" to {_p}
    play sound "block.note_block.pling" with pitch .6 to {_p}
    wait 1 second
    send title colored "" to {_p}



function items_bridge(p: player):
    give {_p} iron sword 
    give {_p} diamond pickaxe of efficiency 1
    give {_p} bow
    give 128 of red terracotta to {_p}
    give 8 of golden apple to {_p}



